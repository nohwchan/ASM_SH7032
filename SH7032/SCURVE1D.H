;*************************************
;--------S CURVE PROCESS-------------*
;*************************************
	MOV D_INCREA,R0		;COUNT TIME FOR CAL. PARABOLA SPEED RANGE
	MOV @R0,R1		;COUNT EVERY 1MS.
	ADD #1,R1
	MOV R1,@R0		

	MOV #10,R2		;COUNT EVERY 10MS.
	CMP/EQ R2,R1
	BF D_NO10MSEC
	MOV D_MSEC10,R3
	MOV @R3,R4
	ADD #1,R4
	MOV R4,@R3
	MOV #0,R5
	MOV R5,@R0
D_NO10MSEC:
	MOV D_MSEC10,R3		;MOVE TIME FOR CAL TO R1
	MOV @R3,R1
	
	MOV DEC_START1,R10
	MOV @R10,R10
	MOV #18,R11
	CMP/GT R11,R10		;IF DEC_START1 > 1.8s
	BT  D_METHOD2		;THEN USING METHOD2(FOR NOT OVERFLOW)
	MULS R1,R1
	STS MACL,R3
	MOV D_KRATE1_,R10
	MOV @R10,R2
	MULS R2,R3
	STS MACL,R3
	SHAR R3
	BRA D_JOIN
	NOP

D_METHOD2:
	MOV D_KRATE1_,R10
	MOV @R10,R2
	MULS R1,R2		;FRUN = 0.5*RATE OF ACCEL*t^2
	STS MACL,R3
	SHAR R3
	MULS R1,R3
	STS MACL,R3
D_JOIN:
	MOV DK1000,R4
	DIVS R4,R3

	MOV INITIALD,R0
	MOV @R0,R4
	SUB R3,R4
	MOV D_FRUNS,R0		;STORE FRUN
	MOV.W R4,@R0
	BRA OUT_S_CURVE1D
	NOP


		.ALIGN 4

D_INCREA: 	.DATA.L D_INCREA_
D_FSETS:	.DATA.L FSET
D_FRUNS:	.DATA.L FRUN
DK1000:		.DATA.L 1000
D_KRATE1_:	.DATA.L _D_KRATE1 ;da/dt
D_MSEC10:	.DATA.L D_MSEC10_
DEC_START1:	.DATA.L _DEC_START_R
OUT_S_CURVE1D:

